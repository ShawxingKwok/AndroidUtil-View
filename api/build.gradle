import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "com.vanniktech.maven.publish"
}

android {
    namespace 'pers.shawxingkwok.androidutil.view'
    compileSdk 33

    defaultConfig {
        minSdk 21
        //noinspection OldTargetApi
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures.viewBinding true
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.fragment:fragment-testing:1.6.1'
}

dependencies {
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'io.github.shawxingkwok:android-util-core:1.0.0'
    implementation 'io.github.shawxingkwok:kt-util:1.0.2'

    testImplementation 'org.jetbrains.kotlin:kotlin-reflect'
}

tasks.withType(KotlinCompile).configureEach{
    kotlinOptions.freeCompilerArgs += "-Xexplicit-api=warning"
    kotlinOptions.freeCompilerArgs += "-Xcontext-receivers"
}

// publish
mavenPublishing {
    def artifactId = "android-util-view"
    def version = "1.0.6"
    def isSnapshot = false

    if (isSnapshot) version += "-SNAPSHOT"
    pom {
        coordinates("io.github.shawxingkwok", artifactId, version)
        def repoName = "AndroidUtil-View"
        name = repoName
        description = "Personal android view util"
        inceptionYear = "2023"

        url = "https://github.com/ShawxingKwok/$repoName/"

        scm{
            connection = "scm:git:git://github.com/ShawxingKwok/${repoName}.git"
            developerConnection = "scm:git:ssh://git@github.com/ShawxingKwok/${repoName}.git"
        }
    }
}